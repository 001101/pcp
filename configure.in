AC_INIT(VERSION)

AC_PREFIX_DEFAULT(/usr)
AC_PACKAGE_GLOBALS(pcp-gui)
AC_PACKAGE_UTILITIES(pcp-gui)
AC_PACKAGE_NEED_QT_QMAKE

pcp_dir=""
if test -n "$PCP_DIR"; then
    pcp_dir="$PCP_DIR"
    export CPPFLAGS="$CPPFLAGS -I$PCP_DIR/include -I$PCP_DIR/local/include"
    export CFLAGS="$CFLAGS -I$PCP_DIR/include -I$PCP_DIR/local/include"
    export LDFLAGS="$LDFLAGS -L$PCP_DIR/bin -L$PCP_DIR/local/bin -L$PCP_DIR/lib"
    export PATH="$PATH:$PCP_DIR/bin:$PCP_DIR/local/bin"
fi
AC_SUBST(pcp_dir)

AC_PACKAGE_NEED_PMAPI_H
AC_PACKAGE_NEED_LIBPCP
AC_PACKAGE_HAVE_PM_SET_PROGNAME

AC_MANUAL_FORMAT

dnl calculate Qt version using qmake binary
eval `$QMAKE --version | awk '{ print $4 }' | awk -F. \
    'END { printf "export QT_MAJOR=%d QT_MINOR=%d QT_POINT=%d\n",$1,$2,$3 }'`

qt_assistant=""		# no longer need this set since 4.4.0
test $QT_MAJOR -lt 4 && qt_assistant=assistant
test -z "$QT_ASSISTANT" || qt_assistant="$QT_ASSISTANT"
AC_SUBST(qt_assistant)

qt_release=release
test -z "$QT_RELEASE" || qt_release="$QT_RELEASE"
AC_SUBST(qt_release)

dnl if regex functions come from libregex (mingw)
AC_CHECK_HEADERS(regex.h)
AC_CHECK_LIB(regex, regcomp)
lib_for_regex=""
if test $ac_cv_lib_regex_regcomp = yes
then
    lib_for_regex="-lregex"
fi
AC_SUBST(lib_for_regex)

dnl check if basename and dirname need -lgen, -lpcp or nothing to work
AC_CHECK_FUNCS(basename)
if test $ac_cv_func_basename = yes
then
    lib_for_basename=""
else
    AC_CHECK_LIB(gen, basename)
    if test $ac_cv_lib_gen_basename = yes
    then
	lib_for_basename="-lgen"
    else
	lib_for_basename="-lpcp"
    fi
fi
AC_SUBST(lib_for_basename)

AC_OUTPUT( \
	src/include/builddefs \
	src/include/version.h \
	src/libqmc/libqmc.pro \
	src/libqwt/libqwt.pro \
	src/libqwt3d/libqwt3d.pro \
	src/dumptext/pmdumptext.pro \
	src/query/pmquery.pro \
	src/chart/pmchart.pro \
	src/time/pmtime.pro \
	man/html/pmchart.adp \
	man/html/pmtime.adp \
	pcp-gui.lsm )
